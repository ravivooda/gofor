// Copyright 2000-2020 JetBrains s.r.o. and other contributors. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.

plugins {
  id 'java'
  id 'org.jetbrains.intellij' version '0.5.0'
  id 'org.jetbrains.changelog' version '0.6.2'
}

group 'org.intellij.sdk'
version '0.0.1'

apply plugin: 'org.jetbrains.changelog'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

test {
  apply plugin: 'jacoco'
  finalizedBy jacocoTestReport
  // Set idea.home.path to the absolute path to the intellij-community source
  // on your local machine.
  systemProperty "idea.home.path", "/Users/jhake/Documents/source/comm"
}

jacocoTestReport {
  dependsOn test // tests are required to run before generating the report
  finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }

    rule {
      enabled = false
      element = 'CLASS'
      includes = ['org.gradle.*']

      limit {
        counter = 'LINE'
        value = 'TOTALCOUNT'
        maximum = 0.3
      }
    }
  }
}

dependencies {
  testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  version = '2020.1.4'
  type = 'IU'
  build {
    plugins = [
            'java',
            'org.jetbrains.plugins.go:201.7846.76.189'
    ]
  }
  patchPluginXml {
    changeNotes({ changelog.getUnreleased().toHTML() })
  }
}

changelog {
  version = "${project.version}"
  path = "${project.projectDir}/CHANGELOG.md"
  header = { "[${project.version}] - ${date()}" }
  headerParserRegex = ~/\d+\.\d+/
  itemPrefix = "-"
  keepUnreleasedSection = true
  unreleasedTerm = "[Unreleased]"
  groups = ["Added", "Changed", "Deprecated", "Removed", "Fixed", "Security"]
}

runIde {
  //apply plugin: 'PsiViewer:201.6251.22-EAP-SNAPSHOT.1'
  ideDirectory '/Applications/GoLand.app/Contents'
}

buildSearchableOptions {
  enabled = false
}

patchPluginXml {
  version = project.version
  sinceBuild = '201'
  untilBuild = '202.*'
}
